class TestViaVerde is subclass of MyTestCase

values
	car: ViaVerde`Car = mk_ViaVerde`Car("12345", mk_(<Mercedes>,<Benz>,<a>));
	
operations
	-- Setup a default environment for the ViaVerde system
	public Setup : () ==> ViaVerde
		Setup () == (
			dcl vv : ViaVerde := new ViaVerde();
			||(
				vv.highways := {"A1" |-> 
					mk_ViaVerde`Highway({<a> |-> 0.1, <b> |-> 0.2, <c> |-> 0.3, <d> |-> 0.4},
					{mk_ViaVerde`Tollbooth(5,<Forward>) |-> 0.4, mk_ViaVerde`Tollbooth(105,<Forward>) |-> 2.5},
						"A1")},
				vv.parks := {"Parque Gaia" |-> 0.5},
				vv.registeredCars := {car |-> mk_ViaVerde`ViaVerdeDevice("0","101010",[])});			
			return vv;
		);
		
	-- Tests with valid inputs	
		
	public testRegisterCar: () ==> ()
	  testRegisterCar() == (
	  	let vv = Setup(),
	  		  newCar = mk_ViaVerde`Car("23456", mk_(<Audi>,<A4>,<a>)),
	  		  vvDevice = mk_ViaVerde`ViaVerdeDevice("1","202020",[])
	  	in (
	  		vv.registerCar(newCar,"202020","1");
	  	  assertEqual(vv.registeredCars(newCar),  vvDevice);
	  	)
	  );
	  
	public testUnregisterCar: () ==> ()
	  testUnregisterCar() == (
	  	let vv = Setup()	  
	  	in (
	  		vv.unregisterCar(car); 
	  	  assertEqual(vv.registeredCars,  {|->});
	  	  assertEqual(vv.unRegisteredCarsRecords,  {car |-> []});
	  	)
	  );
	  
	public testGoThroughHighway: () ==> ()
	  testGoThroughHighway() == (
	  	let vv = Setup(),
	  		  entrykm = 2,
	  		  exitkm = 200,
	  		  entryTime = 1500,
	  		  exitTime = 1630,
	  		  direction = <Forward>,
	  		  entrance = true,
	  		  highway = vv.highways("A1"),
	  		  cost = (exitkm - entrykm) * highway.costPerClass(car.brandModel.#3)
	  	in (
	  		vv.goThroughHighway(car,entrykm,entryTime,direction,entrance,highway);
	  		vv.goThroughHighway(car,exitkm,exitTime,direction,not entrance,highway);
	  		assertEqual(vv.registeredCars(car).records(len vv.registeredCars(car).records).cost, cost);
	  	)
	  );
	  
	public testGoThroughPark: () ==> ()
	  testGoThroughPark() == (
	  	let vv = Setup(),
	  		  entryTime = 1933,
	  		  exitTime = 2000,
	  		  entrance = true,
	  		  cost = (exitTime - entryTime) * vv.parks("Parque Gaia")
	  	in (
	  		vv.goThroughPark(car,entryTime,entrance,"Parque Gaia");
	  		vv.goThroughPark(car,exitTime,not entrance,"Parque Gaia");
	  		assertEqual(vv.registeredCars(car).records(len vv.registeredCars(car).records).cost, cost);
	  	)
	  );
	  
	  public testGoThroughOneTime: () ==> ()
	  testGoThroughOneTime() == (
	  	let vv = Setup(),
	  		  time = 1933,
					type = <SCUT>,
	  		  cost = 2.5,
	  		  record = mk_ViaVerde`OneTimeRecord(type, time) 
	  	in (
	  		vv.goThroughOneTime(car,cost,type,time);
	  		assertEqual(vv.registeredCars(car).records(len vv.registeredCars(car).records).record, record);
	  	)
	  );
	  
	  public testSetCreditCard: () ==> ()
	  testSetCreditCard() == (
	  	let vv = Setup(),
	  		  creditCard = "5555" 
	  	in (
	  		vv.setCreditCard(car,creditCard);
	  		assertEqual(vv.registeredCars(car).creditCard, creditCard);
	  	)
	  );
	  
	  public testAddToRecords: () ==> ()
	  testAddToRecords() == (
	  	let vv = Setup(),
	  		record = mk_ViaVerde`ViaVerdeRecord(mk_ViaVerde`OneTimeRecord(<GasStation>, 1000), 20)
	  	in (
	  		vv.addToRecords(car,record);
	  		assertEqual(vv.registeredCars(car).records, [record]);
	  	)
	  );

	-- Entry point that runs all tests with valid inputs
  public testAll: () ==> ()
  testAll() == (
  	testSetCreditCard();
  	testAddToRecords();
  	testRegisterCar();
  	testUnregisterCar();
  	testGoThroughHighway();
  	testGoThroughPark();
  	testGoThroughOneTime();
  );
  
  -- Tests with invalid inputs

end TestViaVerde

--test retrieveVehicleLocation